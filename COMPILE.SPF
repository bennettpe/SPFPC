/***********************************************************************                                                                                                                                                                                       
                                                                                                                                                                                                                                                               
 (c) Copyright 1992 Command Technology Corporation                                                                                                                                                                                                             
                                                                                                                                                                                                                                                               
 MACRO:     COMPILE - Microsoft C 5.0 (or greater) Compile Macro                                                                                                                                                                                               
                                                                                                                                                                                                                                                               
 PURPOSE:   Compile (C) the program currently being edited and display                                                                                                                                                                                         
            any errors that are found.                                                                                                                                                                                                                         
                                                                                                                                                                                                                                                               
 SIDE       If error messages are found, sets Alt-F5 key to                                                                                                                                                                                                    
 EFFECT:    LOCATE SPECIAL so that the user can use that key                                                                                                                                                                                                   
            to jump between all the error messages found by the                                                                                                                                                                                                
            compiler.  Will not modify the key setting if it has                                                                                                                                                                                               
            been changed from its default.                                                                                                                                                                                                                     
                                                                                                                                                                                                                                                               
            ISPEXEC SHORT and LONG messages are modified to reflect                                                                                                                                                                                            
            outcome of compile.                                                                                                                                                                                                                                
                                                                                                                                                                                                                                                               
 OPERATION: COMPILE starts the C compiler and causes the output to                                                                                                                                                                                             
            be sent to the file "compile.out".  Then, it reads the                                                                                                                                                                                             
            contents of "compile.out" looking for errors associated                                                                                                                                                                                            
            with the current source file.  If any are found, the first                                                                                                                                                                                         
            error for each line is displayed as a NOTE line following                                                                                                                                                                                          
            the line with the error.                                                                                                                                                                                                                           
                                                                                                                                                                                                                                                               
 NOTE:      This MACRO works with Microsoft C Version 5.  It can easily                                                                                                                                                                                        
            be modified for any language that uses the Microsoft error-                                                                                                                                                                                        
            message format.                                                                                                                                                                                                                                    
                                                                                                                                                                                                                                                               
***********************************************************************/                                                                                                                                                                                       
/* Macro to compile the current file using "C" */                                                                                                                                                                                                              
                                                                                                                                                                                                                                                               
'ISREDIT MACRO(PARMS)'                                                                                                                                                                                                                                         
                                                                                                                                                                                                                                                               
trace off                                                                                                                                                                                                                                                      
                                                                                                                                                                                                                                                               
/* Get rid of previous error lines */                                                                                                                                                                                                                          
'ISREDIT RESET SPECIAL'                                                                                                                                                                                                                                        
                                                                                                                                                                                                                                                               
/* If source has not been saved, save it */                                                                                                                                                                                                                    
'ISREDIT (modified) = DATA_CHANGED'                                                                                                                                                                                                                            
if (modified=YES) then                                                                                                                                                                                                                                         
  'ISREDIT SAVE'                                                                                                                                                                                                                                               
                                                                                                                                                                                                                                                               
/* Retrieve filename information about source file */                                                                                                                                                                                                          
'ISREDIT (filename) = DATASET'                                                                                                                                                                                                                                 
filename = TRANSLATE(filename)                                                                                                                                                                                                                                 
                                                                                                                                                                                                                                                               
ADDRESS CMD 'cl /AL /c /nologo /W3 /G2s' filename' 1> compile.out 2> NUL'                                                                                                                                                                                      
                                                                                                                                                                                                                                                               
quote     = "'"                                                                                                                                                                                                                                                
quotes    = quote || '"'                                                                                                                                                                                                                                       
found_one = 0                                                                                                                                                                                                                                                  
                                                                                                                                                                                                                                                               
DO WHILE LINES('COMPILE.OUT')                                                                                                                                                                                                                                  
   dataline1 = LINEIN('COMPILE.OUT')                                                                                                                                                                                                                           
   dataline  = translate(dataline1,'/',quotes)                                                                                                                                                                                                                 
   lastline = lineno                                                                                                                                                                                                                                           
   PARSE VAR dataline errfile '(' lineno ')' errline                                                                                                                                                                                                           
   if (lastline = lineno) | (length(lineno) = 0) then                                                                                                                                                                                                          
      iterate                                                                                                                                                                                                                                                  
   if (errfile \= filename) then                                                                                                                                                                                                                               
      iterate                                                                                                                                                                                                                                                  
   errline = quote || errline || quote                                                                                                                                                                                                                         
   'ISREDIT LINE_BEFORE ' lineno '= NOTELINE 'errline                                                                                                                                                                                                          
END                                                                                                                                                                                                                                                            
                                                                                                                                                                                                                                                               
if found_one then                                                                                                                                                                                                                                              
  do                                                                                                                                                                                                                                                           
    'ISREDIT LOCATE FIRST SPECIAL'                                                                                                                                                                                                                             
                                                                                                                                                                                                                                                               
    ZEDSMSG = "COMPILE ERRORS ENCOUNTERED"                                                                                                                                                                                                                     
    ZEDLMSG = "SYNTAX ERRORS DISCOVERED DURING COMPILE.  SEE INSERTED NOTES"                                                                                                                                                                                   
                                                                                                                                                                                                                                                               
    /* If ALT-F5 is still set to RFIND, then change it to LOCATE SPEC */                                                                                                                                                                                       
    'ISPEXEC VGET ZPF41'                                                                                                                                                                                                                                       
    if (ZPF41=RFIND) then                                                                                                                                                                                                                                      
      do                                                                                                                                                                                                                                                       
        ZPF41 = "LOCATE SPECIAL"                                                                                                                                                                                                                               
        'ISPEXEC VPUT ZPF41'                                                                                                                                                                                                                                   
      end                                                                                                                                                                                                                                                      
  end                                                                                                                                                                                                                                                          
else                                                                                                                                                                                                                                                           
  do                                                                                                                                                                                                                                                           
    ZEDSMSG = "COMPILE SUCCESSFUL"                                                                                                                                                                                                                             
    ZEDLMSG = "COMPILER FOUND NO SYNTAX ERRORS DURING COMPILE."                                                                                                                                                                                                
  end                                                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                               
'ISPEXEC SETMSG MSG(ISRZ000)'                                                                                                                                                                                                                                  
                                                                                                                                                                                                                                                               
