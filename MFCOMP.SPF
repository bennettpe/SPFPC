/**********************************************************************/
/* (c) Copyright 1992 Command Technology Corporation                  */
/*                                                                    */
/* MACRO:     MFCOMP - Micro Focus COBOL Compile Macro                */
/*                                                                    */
/* PURPOSE:   Compile the program currently being edited and display  */
/*            any errors that are found.                              */
/*                                                                    */
/* SIDE       If error messages are found, sets Alt-F5 key to         */
/* EFFECT:    LOCATE SPECIAL so that the user can use that key        */
/*            to jump between all the error messages found by the     */
/*            compiler.                                               */
/*                                                                    */
/* OPERATION: COMPILE starts the Micro Focus Cobol/2 compiler and     */
/*            causes the output to be sent to the file                */
/*            "compile.out".  Then, it reads the contents of          */
/*            "compile.out" looking for errors associated with the    */
/*            current source file.  If any are found, the first       */
/*            error for each line is displayed as a MSG line          */
/*            following the line with the error.                      */
/*                                                                    */
/**********************************************************************/
/* Macro to compile the current file using "COBOL" */

'ISREDIT MACRO(PARMS)'

trace off

/* Get rid of previous error lines */
'ISREDIT RESET SPECIAL'

/* If source has not been saved, save it */
'ISREDIT (modified) = DATA_CHANGED'
if (modified=YES) then
  'ISREDIT SAVE'

/* Retrieve filename information about source file */
'ISREDIT (filename) = DATASET'
filename  = TRANSLATE(filename)

/* Get operating system */
'ispexec vget ZOS'

if (ZOS='DOS') then
  ADDRESS CMD 'COBOL.EXE ' filename ' EDITOR"MS"; >COMPILE.OUT'
else
  ADDRESS CMD 'COBOL.EXE ' filename ' EDITOR"MS"; 2>COMPILE.OUT'

quote     = "'"
quotes    = quote || '"'
found_one = 0

/* Set up errorfile based on operating system */
if (ZOS='DOS') then do
  DO WHILE LINES(COMPILE.OUT) > 0
     dataline1 = LINEIN(COMPILE.OUT)
     say dataline1
     end
  parse var filename fname '.'
  fname=fname||'.MSG'
  end
else
   fname='COMPILE.OUT'

DO WHILE LINES(fname) > 0
   dataline1 = LINEIN(fname)
   dataline  = translate(dataline1,'/',quotes)
   lastline  = lineno
   PARSE VAR dataline errfile lineno errline
   IF (lastline = lineno) | (length(lineno) = 0) THEN
      iterate
   IF (errfile \= filename) THEN
      iterate
   errline   = quote || errline || quote
   found_one = 1
   IF (lineno>0) THEN
    'ISREDIT LINE_BEFORE ' lineno '= MSGLINE 'errline
   ELSE
    'ISREDIT LINE_BEFORE 1 = MSGLINE 'errline
END

/* Display appropriate error/macro done messages */
IF found_one THEN
  do
    'ISREDIT LOCATE FIRST SPECIAL'

    ZEDSMSG = "COMPILE ERRORS ENCOUNTERED"
    ZEDLMSG = "SYNTAX ERRORS DISCOVERED DURING COMPILE.  SEE INSERTED NOTES"

    /* If ALT-F5 is still set to RFIND, then change it to LOCATE SPEC */
    'ISPEXEC VGET ZPF41'
    if (ZPF41=RFIND) then
      do
        ZPF41 = "LOCATE SPECIAL"
        'ISPEXEC VPUT ZPF41'
      end
  end
ELSE
  do
    ZEDSMSG = "COMPILE SUCCESSFUL"
    ZEDLMSG = "COMPILER FOUND NO SYNTAX ERRORS DURING COMPILE."
  end

'ISPEXEC SETMSG MSG(ISRZ000)'
